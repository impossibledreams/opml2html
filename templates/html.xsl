<?xml version="1.0"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:output method = "html"	encoding="utf-8"
  	doctype-system="http://www.w3.org/TR/html4/loose.dtd" 
  	doctype-public="-//W3C//DTD HTML 4.01 Transitional//EN"
  	indent="yes"/>
  	
	<xsl:param name="opml_url"/>
	<xsl:param name="version"/>  	

<!--
#
#   opml2html, created by Yakov Shafranovich.
#   Copyright (c) 2005-2009 SolidMatrix Technologies, Inc.
#   Copyright (c) 2009-2015 Shaftek Enterprises.
#   Copyright (c) 2016- Impossible Dreams Network.
#
#   Source code can be found at:
#   https://github.com/impossibledreams/opml2html
#
-->

	<xsl:template match="/">
 		<html>
 			<head>
 				<title>OPML Diplay: <xsl:value-of select="opml/head/title"/></title>
 				<script src="includes/listCollapse.js" type="text/javascript" language="javascript1.2"></script> 				
				<xsl:comment>
					Generated by <xsl:value-of select="$version"/> 
					opml2html itself (not the output) is copyright (C) 2005 SolidMatrix Technologies, Inc.
					opml2html itself (not the output) is licensed under the Apache License, Version 2.0.
				</xsl:comment> 			
 			</head>
 			<body onLoad="compactMenu('list',true,'&amp;plusmn; ');">
 				<hr/>
				<h1 align="left">OPML Display: <xsl:value-of select="opml/head/title"/></h1>
				<h3 align="left">OPML Location: <I><a href="{$opml_url}"><xsl:value-of select="$opml_url"/></a></I></h3>
				<hr/>
 				<ul id="list">
 		 			<xsl:apply-templates/> 
 		 		</ul> 		 		
 		 		<hr/>
 		 		<p align="center">Generated by <xsl:value-of select="$version"/>. Source code available at <a href="https://github.com/impossibledreams/opml2html">GitHub</a>.
 		 		Collapsible list script provided for free by <a href="http://www.howtocreate.co.u">http://www.howtocreate.co.uk</a>.
 		 		</p>
 		 		<p align="center">
    				<a href="http://validator.w3.org/check?uri=referer">
    					<img
        				src="http://www.w3.org/Icons/valid-html401"
        				alt="Valid HTML 4.01 Transitional" height="31" width="88"/>
        			</a>
  				</p>
  				<hr/>
			</body>
		</html>
	</xsl:template>

	<xsl:template match="outline">
	    <xsl:variable name="text"><xsl:choose>
	        <xsl:when test="@text"><xsl:value-of select="@text"/></xsl:when>
		<xsl:otherwise><xsl:value-of select="@title"/></xsl:otherwise>
	    </xsl:choose></xsl:variable>
					
		<xsl:choose>
			<xsl:when test="@type = 'link'">
			    <li>
				<a href="{@url}"><xsl:value-of select="$text"/></a>
				<xsl:if test="@adUrl"> - <small><a href="{@adUrl}">(Buy)</a></small></xsl:if>
			    </li>
			</xsl:when>
			<xsl:when test="@type = 'rss' and string-length(@url) &gt; 0">
			    <li>
				<a href="{@url}"><xsl:value-of select="$text"/></a>
				<xsl:if test="@adUrl"> - <small><a href="{@adUrl}">(Buy)</a></small></xsl:if>
			    </li>
			</xsl:when>
			<xsl:when test="@type = 'rss' and string-length(@htmlUrl) &gt; 0">
			    <li>
				<a href="{@htmlUrl}"><xsl:value-of select="$text"/></a>
				<xsl:if test="@adUrl"> - <small><a href="{@adUrl}">(Buy)</a></small></xsl:if>
			    </li>
			</xsl:when>													
			<xsl:otherwise>
				<li><xsl:value-of select="$text"/>
					<ul>
						<xsl:apply-templates/>
					</ul>
				</li>
			</xsl:otherwise>			
		</xsl:choose>
	</xsl:template>

	<xsl:template match="text()">
	</xsl:template>	
	
</xsl:stylesheet>
